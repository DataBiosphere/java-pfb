plugins {
    id 'com.srcclr.gradle'
    id 'maven-publish'
    id 'com.jfrog.artifactory'
    id 'bio.terra.pfb.java-library-conventions'
    id 'com.github.davidmc24.gradle.plugin.avro' version '1.9.1'
    id 'me.champeau.jmh' version '0.7.2'
}

repositories {
    maven {
        // Terra proxy for maven central
        url 'https://broadinstitute.jfrog.io/broadinstitute/maven-central/'
    }
    mavenCentral()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release/'
    }
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-release-local/'
    }
}

dependencies {
    api "org.apache.avro:avro:1.11.3"
    implementation 'ch.qos.logback:logback-classic:1.5.3'
    testImplementation 'org.json:json:20240303'
    testImplementation 'com.fasterxml.jackson.core:jackson-databind:2.17.0'
    testImplementation 'org.skyscreamer:jsonassert:1.5.1'
}

sourceSets.main.java.srcDirs += ["${buildDir}/generated-src"]
spotless {
    java{
        // Specifically calling out the "build/generated-src" directory
        // to avoid spotless errors on avro generated code in GHA
        // ${buildDir}/** doesn't work in GHA
        targetExclude "${buildDir}/**", "build/generated-src/**"
    }
}

generateAvroJava {
    source("library/src/main/avro")
    outputDir = file("build/generated-src")
}
compileJava.dependsOn generateAvroJava

apply from: 'generators.gradle'
apply from: "publishing.gradle"

srcclr {
    scope = "runtimeClasspath"
}

sourcesJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

jmh {
    iterations = 2 // Number of measurement iterations to do.
    fork = 2 // How many times to forks a single benchmark. Use 0 to disable forking altogether
    operationsPerInvocation = 3 // Operations per invocation.
    warmup = '1s' // Time to spend at each warmup iteration.
    warmupBatchSize = 10 // Warmup batch size: number of benchmark method calls per operation.
    warmupForks = 0 // How many warmup forks to make for a single benchmark. 0 to disable warmup forks.
    warmupIterations = 1 // Number of warmup iterations to do.
    resultFormat = 'JSON'
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt") // human-readable output file
    resultsFile = project.file("${project.buildDir}/reports/jmh/results.json") // results file
}
